вкл прелюдия;
вкл рейлиб;

конст ЦВЕТ_ЗАДНЕГО_ФОНА := 16%ФФ211816нат;
конст ЦВЕТ_ПЕРЕДНЬЕГО_ФОНА := 16%ФФ957260нат;

конст ШИРИНА_ОКНА := 800;
конст ВЫШИНА_ОКНА := 800;

конст ЦЕНТР_ОКНА_ИКС := (ШИРИНА_ОКНА как вещ) / 2.0;
конст ЦЕНТР_ОКНА_ИГРЕК := (ВЫШИНА_ОКНА как вещ) / 2.0;

конст ДИАМЕТР_КОЛЬЦА := ШИРИНА_ОКНА - 120;

конст СИЛА_ОТСКОКА := 15.0;

конст ПИ := 3.14159265358979323846;

про корень(икс: вещ): вещ внешняя "sqrtf";

структ Мяч нч
  икс: вещ;
  игрек: вещ;

  скорость_икс: вещ;
  скорость_игрек: вещ;

  радиус: вещ;

  площадь: вещ;
кц

про нарисовать_мячик(мячик: Мяч) нч
  нарисовать_круг(мячик.икс как цел, мячик.игрек как цел, (мячик.радиус * 2.0) как цел, ЦВЕТ_ПЕРЕДНЬЕГО_ФОНА);
кц


про главная() нч
  // пер номер: нат := 30нат;
  // печать(номер, " Привет, мир\н");
  //
  // для индекс := 0..10 нч
  //   печать("Номер ", индекс, "\н");
  // кц

  пер мячик: Мяч;
  мячик.радиус := 25.0;
  мячик.площадь := 100.0;
  мячик.икс := (ШИРИНА_ОКНА как вещ) / 2.0 + 190.0;
  мячик.игрек := (ВЫШИНА_ОКНА как вещ) / 2.0;
  мячик.скорость_икс := 0.0;
  мячик.скорость_игрек := 0.0;
  
  открыть_окно(ШИРИНА_ОКНА, ВЫШИНА_ОКНА, адрес("Скачущий мячик"));
  установить_целевую_частоту_кадров(60);

  пока !пора_закрыть_окно() нч
    начать_рисовать();
    очистить_фон(ЦВЕТ_ЗАДНЕГО_ФОНА);

    нарисовать_мячик(мячик);
    мячик.икс := мячик.икс + мячик.скорость_икс;
    мячик.игрек := мячик.игрек + мячик.скорость_игрек;

    мячик.скорость_игрек := мячик.скорость_игрек + 0.3;

    пер икс_расс: вещ := ((ШИРИНА_ОКНА как вещ) / 2.0) - (мячик.икс);
    пер игрек_расс: вещ := ((ВЫШИНА_ОКНА как вещ) / 2.0) - (мячик.игрек);
    пер расстояние: вещ := корень(икс_расс * икс_расс + игрек_расс * игрек_расс);

    пер цельевая_глубина: вещ := ((ДИАМЕТР_КОЛЬЦА как вещ) / 2.0) - мячик.радиус;
    // печать(расстояние как цел, " - ", цельевая_глубина как цел, "\н");

    если расстояние +? цельевая_глубина нч
      пер нормализованный_икс: вещ := икс_расс / расстояние;
      пер нормализованный_игрек: вещ := игрек_расс / расстояние;
      пер глубина: вещ := цельевая_глубина - расстояние;
      //печать(глубина как цел, "\н");

      мячик.скорость_икс := нормализованный_икс * СИЛА_ОТСКОКА;//-глубина * 1.6;
      мячик.скорость_игрек := нормализованный_игрек * СИЛА_ОТСКОКА;//-глубина * 1.6;

      мячик.икс := ЦЕНТР_ОКНА_ИКС - нормализованный_икс * цельевая_глубина;
      мячик.игрек := ЦЕНТР_ОКНА_ИГРЕК - нормализованный_игрек * цельевая_глубина;

      мячик.площадь := мячик.площадь + 120.0;
      мячик.радиус := корень(мячик.площадь / ПИ);

      // икс_расс   := (мячик.икс) - ((ШИРИНА_ОКНА как вещ) / 2.0);
      // игрек_расс := (мячик.игрек) - ((ВЫШИНА_ОКНА как вещ) / 2.0);
      // расстояние := корень(икс_расс * икс_расс + игрек_расс * игрек_расс);
      // нормализованный_икс := икс_расс / расстояние;
      // нормализованный_игрек := игрек_расс / расстояние;
      // глубина := ((ДИАМЕТР_КОЛЬЦА как вещ) / 2.0) - мячик.радиус;
    кц

    нарисовать_кольцо(ШИРИНА_ОКНА / 2, ВЫШИНА_ОКНА / 2, ДИАМЕТР_КОЛЬЦА, ДИАМЕТР_КОЛЬЦА + 20, ЦВЕТ_ПЕРЕДНЬЕГО_ФОНА);

    закончить_рисовать();
  кц

  закрыть_окно();
кц
